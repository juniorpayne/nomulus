// Copyright 2019 The Nomulus Authors. All Rights Reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

sourceSets {
  testing {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
  test {
    java {
      compileClasspath += testing.output
      runtimeClasspath += testing.output
    }
  }
}

configurations {
  testingCompile.extendsFrom compile
  testingRuntime.extendsFrom runtime

  testCompile.extendsFrom testingCompile
  testRuntime.extendsFrom testingRuntime

  // All testing util classes. Other projects may declare dependency as:
  // testCompile project(path: 'common', configuration: 'testing')
  testing
}

task testingJar(type: Jar) {
  archiveBaseName = 'testing'
  from sourceSets.testing.output
}

artifacts {
  testing testingJar
}

dependencies {
  def deps = rootProject.dependencyMap

  compile deps['com.google.code.findbugs:jsr305']
  compile deps['com.google.guava:guava']
  compile deps['javax.inject:javax.inject']
  compile deps['joda-time:joda-time']

  testingCompile deps['com.google.flogger:flogger']
  testingRuntime deps['com.google.flogger:flogger-system-backend']
  testingCompile deps['com.google.truth:truth']
  testingCompile deps['io.github.java-diff-utils:java-diff-utils']

  testCompile deps['org.junit.jupiter:junit-jupiter-api']
  testCompile deps['org.junit.jupiter:junit-jupiter-engine']
}
